apply plugin: 'com.android.library'
apply plugin: 'maven'

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }

    lintOptions {
        disable 'InvalidPackage', 'AllowBackup'
    }
    buildTypes {
        debug {
            testCoverageEnabled = true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

apply plugin: 'checkstyle'

check.dependsOn 'checkstyle'

task checkstyle(type: Checkstyle) {
    configFile file("../checkstyle.xml")
    source 'src'
    include 'main/**/*.java'
    exclude '**/generated/**',
            '**/extensions/**',
            '**/core/OneDriveErrorCodes.java',
            '**/http/OneDrive*Error*.java',
            '**/authenication/ServiceInfo.java',
            '**/authenication/DiscoveryServiceResponse.java'
    ignoreFailures = false
    classpath = files()
}

dependencies {
    compile ('com.microsoft.services.msa:msa-auth:0.8.4') {
        exclude module: 'com.microsoft.services.msa'
    }
    compile ('com.microsoft.aad:adal:1.1.7') {
        exclude module: 'com.microsoft.aad'
    }
    compile ('com.google.code.gson:gson:2.3.1') {
        exclude module: 'com.google.code.gson'
    }
}

uploadArchives {
    def bintrayUsername = ""
    def bintrayApikey = ""
    if (project.rootProject.file('local.properties').exists()) {
        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        bintrayUsername = properties.getProperty('bintray.user')
        bintrayApikey = properties.getProperty('bintray.apikey')
    }

    configuration = configurations.archives
    repositories.mavenDeployer {
        pom {
           setGroupId project.mavenGroupId
           setArtifactId project.mavenArtifactId
           setVersion project.mavenVersion
        }
        repository (url: project.mavenRepoUrl) {
            authentication(
                 // put these values in local file ~/.gradle/gradle.properties
                 userName: project.hasProperty("bintrayUsername") ? project.bintrayUsername : bintrayUsername,
                 password: project.hasProperty("bintrayApikey") ? project.bintrayApikey : bintrayApikey
            )
        }
    }
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath())
    classpath += configurations.compile
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives javadocJar
    archives sourcesJar
}
